buildscript {
  ext.getExtOrDefault = {name ->
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['MendixNative_' + name]
  }

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.7.2"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
  }
}


apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: 'kotlin-kapt'
apply plugin: "com.facebook.react"

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["MendixNative_" + name]).toInteger()
}

android {
  namespace "com.mendixnative"

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
  }

  buildFeatures {
    dataBinding true
    viewBinding true
    buildConfig true
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }

  sourceSets {
    main {
      java.srcDirs += [
        "generated/java",
        "generated/jni"
      ]
    }
  }
}

repositories {
  mavenCentral()
  google()
}

def kotlin_version = getExtOrDefault("kotlinVersion")

dependencies {
  implementation "com.facebook.react:react-android"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  implementation 'com.fasterxml.jackson.core:jackson-core:2.11.3'
  implementation 'com.fasterxml.jackson.core:jackson-annotations:2.11.3'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.3'

  implementation 'androidx.security:security-crypto:1.1.0-alpha03'

  implementation "com.github.bumptech.glide:glide:4.12.0"
  kapt "android.arch.lifecycle:compiler:1.1.1"
  kapt 'com.github.bumptech.glide:compiler:4.12.0'


  api "com.facebook.react:react-android:0.77.3"
  api project(':op-engineering_op-sqlite')
  api project(':react-native-async-storage_async-storage')
  api project(':react-native-gesture-handler')
}
